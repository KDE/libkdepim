# SPDX-FileCopyrightText: none
# SPDX-License-Identifier: BSD-3-Clause
cmake_minimum_required(VERSION 3.16 FATAL_ERROR)
set(PIM_VERSION "6.6.40")

project(libkdepim VERSION ${PIM_VERSION})

set(KF_MIN_VERSION "6.18.0")
set(QT_REQUIRED_VERSION "6.8.0")
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_SCAN_FOR_MODULES OFF)
find_package(ECM ${KF_MIN_VERSION} CONFIG REQUIRED)
set(CMAKE_MODULE_PATH ${ECM_MODULE_PATH})

include(KDEInstallDirs)
include(KDECMakeSettings)
include(KDECompilerSettings NO_POLICY_SCOPE)

include(ECMGenerateExportHeader)
include(ECMSetupVersion)
include(ECMGenerateHeaders)

file(GLOB_RECURSE ALL_CLANG_FORMAT_SOURCE_FILES src/*.cpp src/*.h)
set(PRE_COMMIT_INSTALLED FALSE)
if(${CMAKE_SOURCE_DIR} STREQUAL ${PROJECT_SOURCE_DIR})
    if(EXISTS "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit")
        file(READ "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit" FILE_CONTENTS)
        string(FIND "${FILE_CONTENTS}" "File generated by pre-commit" INDEX)
        if(${INDEX} GREATER_EQUAL 0)
            set(PRE_COMMIT_INSTALLED TRUE)
        endif()
    endif()
endif()
if(NOT ${PRE_COMMIT_INSTALLED})
    include(KDEGitCommitHooks)
    include(KDEClangFormat)
    kde_clang_format(${ALL_CLANG_FORMAT_SOURCE_FILES})
endif()

include(ECMQtDeclareLoggingCategory)
include(ECMDeprecationSettings)
include(ECMFeatureSummary)
include(ECMAddTests)
include(ECMAddQch)
include(ECMCheckOutboundLicense)
ecm_check_outbound_license(LICENSES GPL-2.0-only  FILES ${ALL_CLANG_FORMAT_SOURCE_FILES})

option(
    FORCE_NOT_REQUIRED_DEPENDENCIES
    "List (semicolon-separated) of dependencies that will be downgraded from REQUIRED to RECOMMENDED"
)

function(set_libkdepim_optional_package_properties _name _props)
    if(NOT "${_props}" STREQUAL "PROPERTIES")
        message(
            FATAL_ERROR
            "PROPERTIES keyword is missing in set_libkdepim_optional_package_properties() call."
        )
    endif()

    set(options) # none
    set(oneValueArgs DESCRIPTION URL PURPOSE)
    set(multiValueArgs) # none

    cmake_parse_arguments(
        _SPP
        "${options}"
        "${oneValueArgs}"
        "${multiValueArgs}"
        ${ARGN}
    )

    if(_SPP_UNPARSED_ARGUMENTS)
        message(
            FATAL_ERROR
            "Unknown keywords given to set_libkdepim_optional_package_properties(): \"${_SPP_UNPARSED_ARGUMENTS}\""
        )
    endif()

    set(DEPENDENCY_TYPE "REQUIRED")
    if(${_name} IN_LIST FORCE_NOT_REQUIRED_DEPENDENCIES)
        set(DEPENDENCY_TYPE "RECOMMENDED")
    endif()
    set_package_properties(
        ${_name}
        PROPERTIES
            TYPE ${DEPENDENCY_TYPE}
            DESCRIPTION ${_SPP_DESCRIPTION}
            URL ${_SPP_URL}
            PURPOSE
                "${_SPP_PURPOSE} You can make the dependency optional adding ${_name} to the FORCE_NOT_REQUIRED_DEPENDENCIES cmake option"
    )
endfunction()

find_package(Qt6DBus ${QT_REQUIRED_VERSION})
set_libkdepim_optional_package_properties(Qt6DBus PROPERTIES
    PURPOSE "Required for the DBUS interfaces delegate."
)

option(
    BUILD_QCH
    "Build API documentation in QCH format (for e.g. Qt Assistant, Qt Creator & KDevelop)"
    OFF
)
add_feature_info(
    QCH
    ${BUILD_QCH}
    "API documentation in QCH format (for e.g. Qt Assistant, Qt Creator & KDevelop)"
)

set(LIBKDEPIM_LIB_VERSION ${PIM_VERSION})
find_package(Qt6 ${QT_REQUIRED_VERSION} CONFIG REQUIRED Widgets Network)

if(BUILD_TESTING)
    find_package(Qt6 ${QT_REQUIRED_VERSION} CONFIG REQUIRED Test)
endif()

find_package(KF6Completion ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6I18n ${KF_MIN_VERSION} CONFIG REQUIRED)
find_package(KF6WidgetsAddons ${KF_MIN_VERSION} CONFIG REQUIRED)

option(BUILD_DESIGNERPLUGIN "Build plugin for Qt Designer" ON)
add_feature_info(
    DESIGNERPLUGIN
    ${BUILD_DESIGNERPLUGIN}
    "Build plugin for Qt Designer"
)

add_definitions(-DQT_NO_CONTEXTLESS_CONNECT)
ecm_set_disabled_deprecation_versions(QT 6.10.0 KF 6.20.0)

if(BUILD_TESTING)
    add_definitions(-DBUILD_TESTING)
endif()

########### CMake Config Files ###########
set(CMAKECONFIG_INSTALL_DIR "${KDE_INSTALL_CMAKEPACKAGEDIR}/KPim6Libkdepim")

if(TARGET Qt6::DBus)
    configure_package_config_file(
        KPimMailTransportDBusServiceConfig.cmake.in
        ${CMAKE_CURRENT_BINARY_DIR}/KPim6MailTransportDBusServiceConfig.cmake
        PATH_VARS KDE_INSTALL_DBUSINTERFACEDIR
        INSTALL_DESTINATION
            "${KDE_INSTALL_CMAKEPACKAGEDIR}/KPim6MailTransportDBusService"
    )

    install(
        FILES
            ${CMAKE_CURRENT_BINARY_DIR}/KPim6MailTransportDBusServiceConfig.cmake
        DESTINATION
            "${KDE_INSTALL_CMAKEPACKAGEDIR}/KPim6MailTransportDBusService"
    )
endif()

option(
    USE_UNITY_CMAKE_SUPPORT
    "Use UNITY cmake support (speedup compile time)"
    OFF
)

set(COMPILE_WITH_UNITY_CMAKE_SUPPORT OFF)
if(USE_UNITY_CMAKE_SUPPORT)
    set(COMPILE_WITH_UNITY_CMAKE_SUPPORT ON)
endif()

add_subdirectory(src)

set(CMAKECONFIG_INSTALL_DIR "${KDE_INSTALL_CMAKEPACKAGEDIR}/KPim6Libkdepim")

if(BUILD_QCH)
    ecm_install_qch_export(
        TARGETS KPim6Libkdepim_QCH
        FILE KPim6LibkdepimQchTargets.cmake
        DESTINATION "${CMAKECONFIG_INSTALL_DIR}"
        COMPONENT Devel
    )
    set(PACKAGE_INCLUDE_QCHTARGETS
        "include(\"\${CMAKE_CURRENT_LIST_DIR}/KPim6LibkdepimQchTargets.cmake\")"
    )
endif()

configure_package_config_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/KPimLibkdepimConfig.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/KPim6LibkdepimConfig.cmake"
    INSTALL_DESTINATION ${CMAKECONFIG_INSTALL_DIR}
)

install(
    FILES
        "${CMAKE_CURRENT_BINARY_DIR}/KPim6LibkdepimConfig.cmake"
        "${CMAKE_CURRENT_BINARY_DIR}/src/KPim6LibkdepimConfigVersion.cmake"
    DESTINATION "${CMAKECONFIG_INSTALL_DIR}"
    COMPONENT Devel
)

install(
    EXPORT KPim6LibkdepimTargets
    DESTINATION "${CMAKECONFIG_INSTALL_DIR}"
    FILE KPim6LibkdepimTargets.cmake
    NAMESPACE KPim6::
)

ecm_qt_install_logging_categories(
        EXPORT LIBKDEPIM
        FILE libkdepim.categories
        DESTINATION ${KDE_INSTALL_LOGGINGCATEGORIESDIR}
)

if(DEFINED kde_configure_git_pre_commit_hook)
    kde_configure_git_pre_commit_hook(CHECKS CLANG_FORMAT)
endif()

ki18n_install(po)

ecm_feature_summary(WHAT ALL FATAL_ON_MISSING_REQUIRED_PACKAGES)
